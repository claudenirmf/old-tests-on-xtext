@Ecore(nsURI="http://www.nemo.inf.ufes.br/ontol/OntoL")
@GenModel(modelDirectory="br.ufes.inf.nemo.ontol.model/src-gen")
package br.ufes.inf.nemo.ontol.model

class Model
{
	String name
	refers Model[] includes
	contains ModelElement[] elements
}

abstract class ModelElement {}

class Import extends ModelElement
{
	String importedNamespace
}

abstract class EntityDeclaration extends ModelElement 
{
	String name
	refers OntoLClass[] instantiatedClasses
	contains AttributeAssignment[] attAssignments
	contains ReferenceAssignment[] refAssignments
}

class Individual extends EntityDeclaration {}

abstract class OntoLClass extends EntityDeclaration 
{
	refers OntoLClass[] superClasses
	refers OntoLClass powertypeOf
	refers OntoLClass basetype
	refers OntoLClass[] subordinators
	contains Attribute[] attributes
	contains Reference[] references
	CategorizationType[0..1] categorizationType
}

enum CategorizationType 
{
	CATEGORIZER as "categorizes"
	DISJOINT_CATEGORIZER as "disjointCategorizes"
	COMPLETE_CATEGORIZER as "completeCategorizes"
	PARTITIONER as "partitions"
}

class OrderlessClass extends OntoLClass {}

abstract class OrderedClass extends OntoLClass {}

class HOClass extends OrderedClass
{
	Integer order
}

class FOClass extends OrderedClass {}

class GeneralizationSet extends ModelElement 
{
	String name = "anonymous"
	boolean isDisjoint = "false"
	boolean isComplete = "false"
	refers OntoLClass general
	refers OntoLClass categorizer
	refers OntoLClass[] specifics
}

class Property extends ModelElement 
{
	String name
	Integer lowerBound = "1"
	Integer upperBound = "1"
	refers OntoLClass propertyType
	
	Boolean regularity = "false"
	RegularityPropertyType regularityType
	refers Property regulatedProperty
}

enum RegularityPropertyType 
{
	DETERMINES_MAX_VALUE as "determinesMaxValue"
	DETERMINES_MIN_VALUE as "determinesMinValue"
	DETERMINES_VALUE as "determinesValue"
	DETERMINES_POSSIBLE_VALUES as "determinesPossibleValues"
	DETERMINES_TYPE as "determinesType"
	DETERMINES_POSSIBLE_TYPES as "determinesPossibleTypes"
}

class Attribute extends Property 
{
	refers Attribute[] subsetOf
}

class Reference extends Property 
{
	refers Reference[] subsetOf
	refers Reference oppositeTo
}

class PropertyAssignment extends ModelElement {}

class AttributeAssignment extends PropertyAssignment
{
	refers Attribute attribute
	contains DataValue[] assignments
}

class ReferenceAssignment extends PropertyAssignment
{
	refers Reference reference
	refers EntityDeclaration[] assignments
}

abstract class DataValue {}

class StringValue extends DataValue 
{
	String value
}

class NumberValue extends DataValue
{
	double value
}

class BooleanValue extends DataValue
{
	boolean value
}

class NoneValue extends DataValue
{
	op Object getValue() { return null }
}

class ComplexDataValue extends DataValue {
	refers Individual value
	contains Individual unnamedValue
}
